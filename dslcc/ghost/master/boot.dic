OnFirstBoot
{
	//If you add variables, make sure to initialize them here!
	//Note - I haven't added anything about username or pronouns here because it's all handled by the SSP prefs
	stroke = 0
	LastTalk = ""
	
	"\1\s[2]\p[2]\s[3]\p[3]\s[4]\0\s[11]Ah,\w2 you're here!\w7\s[0] Come in,\w3 come in,\w5\s[17] would you like some hot chocolate?\e"
}

OnInitialize
{
	//If you want some variables to reset on boot, put them here. This event will happen even if you add other events like OnGhostChanged
	//OnInitialize is a NOTIFY event! That means it cannot return scripts. This event is only meant for things like variable changes. See this page on Ukadoc for more info: https://ukagakadreamteam.github.io/ukadoc/manual/spec_shiori3.html
	
	//If you add new variables in updates, I recommend initializing them like so:
	//if !ISVAR("NewVariable"); NewVariable = 0
	//This means that it will only set the new variable if the user doesn't already have it, so that their variables don't get overwritten
	
	if reference0 != "reload" //For things you want to reset EXCEPT when just reloading; a convenience for you the dev, basically
	{
		LastTalk = ""
	}
}

OnBoot
{
	_temp = SHIORI3FW.UserBirthday
	_user_birthday = "%(_temp[1])/%(_temp[2])" //M/D, if you want it D/M you can switch it around
	_today = "%(month)/%(day)" //Again, switch this around if using D/M
	
	"\1\s[2]\0\s[0]\p[2]\s[3]\p[3]\s[4]"
	--
	if _today == _user_birthday
	{
		"\0\s[11]HAPPY BIRTHDAY!\s[15] I made you a plushie! It's a %(plush) from a new pattern I found.\e"
	}
	elseif _today == "1/1" //You can add whatever cool dates you want with more elseifs here
	{
		"\0\s[11]Happy new year %(username)."
	}
	else //Normal boot
	{
		"\0\s[0]Hello again,\w4\s[11] would you like something to drink?\e"
	}
}

OnClose
{
	"\0\s[0]You have to go?\w6 Well,\s[11] I hope we get to do this again soon."
	--
	"\w8\w8\-" //This is what makes the ghost close (after a short pause), do not remove the \- , and MAKE SURE you don't use a \e before this point or it won't work
}